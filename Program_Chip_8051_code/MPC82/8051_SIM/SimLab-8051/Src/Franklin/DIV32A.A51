;
; M(32位元)/N(16位元)=Q(32位元)...R(16位元)(binary unsigned view) 
;
	DSEG	AT 40H
MM:	DS	4
MN:	DS	2
MQ:	DS	4
MR:	DS	2
MT:	DS	6

	CSEG
START:
; 測試資料(12345678H/234H)=? (84354H....168H) 

	MOV	MM,#12H
	MOV	MM+1,#34H
	MOV	MM+2,#56H
	MOV	MM+3,#78H
	MOV	MN,#02H
	MOV	MN+1,#34H

	SETB  F0
; T<--2**32*N
	MOV	MT,MN
	MOV	MT+1,MN+1
	MOV	MT+2,#0
	MOV	MT+3,#0
	MOV	MT+4,#0
	MOV	MT+5,#0

	MOV	R0,#32
LOOP1:
;  T <-T/2
	MOV	A,MT
	CLR	C
	RRC	A
	MOV	MT,A
	MOV	A,MT+1
	RRC	A
	MOV	MT+1,A
	MOV	A,MT+2
	RRC	A
	MOV	MT+2,A
	MOV	A,MT+3
	RRC	A
	MOV	MT+3,A
	MOV	A,MT+4
	RRC	A
	MOV	MT+4,A
	MOV	A,MT+5
	RRC	A
	MOV	MT+5,A
;      M >=T
	MOV	A,MT
	ORL	A,MT+1
	JNZ	LESS1
	MOV	A,MM
	CJNE	A,MT+2,NEXT2
	MOV	A,MM+1
	CJNE	A,MT+3,NEXT2
	MOV	A,MM+2
	CJNE	A,MT+4,NEXT2
	MOV	A,MM+3
	CJNE	A,MT+5,NEXT2
	SJMP	GREATER

NEXT2:
	JC	LESS1
GREATER:
; M<-M-T
	MOV	A,MM+3
	CLR	C
	SUBB	A,MT+5
	MOV	MM+3,A
	MOV	A,MM+2
	SUBB	A,MT+4
	MOV	MM+2,A
	MOV	A,MM+1
	SUBB	A,MT+3
	MOV	MM+1,A
	MOV	A,MM+0
	SUBB	A,MT+2
	MOV	MM+0,A
	SETB	C
	SJMP	CON1
LESS1:
	CLR	C
CON1:
	MOV	A,MQ+3
	RLC	A
	MOV	MQ+3,A
	MOV	A,MQ+2
	RLC	A
	MOV	MQ+2,A
	MOV	A,MQ+1
	RLC	A
	MOV	MQ+1,A
	MOV	A,MQ+0
	RLC	A
	MOV	MQ+0,A
	DJNZ	R0,LOOP1

	MOV	MR,MM+2
	MOV	MR+1,MM+3

	SETB	F0
	AJMP	$
	END
