;
;
;   Display Chinese word "huan" (three color) on 16*16 Color Dot-matrix LED
;
BASEINT	EQU	25
INTERVAL1	EQU	BASEINT*1
INTERVAL2	EQU	BASEINT*2
INTERVAL3	EQU	BASEINT*3

IDX	REG	R2
INTERVAL	REG	R5
COLOR	REG	R6
CTRLLED	REG	P2.0
LRED1	REG	P1.4
LGREEN1	REG	P1.5
LRED2	REG	P1.6
LGREEN2	REG	P1.7

START:
	MOV	IDX,#0
	MOV	INTERVAL,#0
	MOV	COLOR,#0
	MOV	DPTR,#HWAN
LOOP:
	SETB	CTRLLED
	CJNE	COLOR,#0,CHKCOLOR1
	SJMP	CHKCOLOR2
CHKCOLOR1:
	CJNE	COLOR,#2,CHKCOLOR3
CHKCOLOR2:
	MOV	A,IDX
	ADD	A,ACC
	MOVC	A,@A+DPTR
	MOV	P0,A
	CLR	LRED1
	SETB	LRED1

	MOV	A,IDX
	ADD	A,ACC
	INC	A
	MOVC	A,@A+DPTR
	MOV	P0,A
	CLR	LRED2
	SETB	LRED2
	SJMP	CHKCOLOR4
CHKCOLOR3:
	MOV	P0,#0FFH
	CLR	LRED1
	SETB	LRED1
	CLR	LRED2
	SETB	LRED2
CHKCOLOR4:

	CJNE	COLOR,#1,CHKCOLOR5
	SJMP	CHKCOLOR6
CHKCOLOR5:
	CJNE	COLOR,#2,CHKCOLOR7
CHKCOLOR6:
	MOV	A,IDX
	ADD	A,ACC
	MOVC	A,@A+DPTR
	MOV	P0,A
	CLR	LGREEN1
	SETB	LGREEN1

	MOV	A,IDX
	ADD	A,ACC
	INC	A
	MOVC	A,@A+DPTR
	MOV	P0,A
	CLR	LGREEN2
	SETB	LGREEN2
	SJMP	CHKCOLOR8
CHKCOLOR7:
	MOV	P0,#0FFH
	CLR	LGREEN1
	SETB	LGREEN1
	CLR	LGREEN2
	SETB	LGREEN2
CHKCOLOR8:
	MOV	A,P1
	ANL	A,#0F0H
	ORL	A,IDX
	MOV	P1,A
	CLR	CTRLLED

	INC	IDX

	CJNE	IDX,#16,CHKINTERVAL
	MOV	IDX,#0
	INC	INTERVAL

CHKINTERVAL:
	CJNE	INTERVAL,#INTERVAL1,CHKINTERVAL1
	MOV	COLOR,#1
	SJMP	DELAY
CHKINTERVAL1:
	CJNE	INTERVAL,#INTERVAL2,CHKINTERVAL2
	MOV	COLOR,#2
	SJMP	DELAY
CHKINTERVAL2:
	CJNE	INTERVAL,#INTERVAL3,DELAY
	MOV	INTERVAL,#0
	MOV	COLOR,#0

DELAY:
	ACALL	DELAY1MS

	AJMP	LOOP

	include	delay.inc

HWAN:
	DB	0FFH,0FFH
	DB	0DFH,0EFH
	DB	0D1H,09FH
	DB	084H,000H
	DB	0D1H,055H
	DB	0DEH,055H
	DB	0D1H,001H
	DB	085H,055H
	DB	0D1H,055H
	DB	0DEH,0FEH
	DB	0F9H,0F9H
	DB	087H,0E7H
	DB	0B4H,01FH
	DB	0F7H,0E7H
	DB	0F5H,0F9H
	DB	0E3H,0FEH
	END
