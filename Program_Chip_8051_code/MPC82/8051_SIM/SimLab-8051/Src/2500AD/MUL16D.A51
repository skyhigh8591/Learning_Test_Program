;
; 兩個16位元數的相乘(binary unsigned view) 
;
MA:	EQU	40H
MB:	EQU	MA+2
MP:	EQU	MB+2
ALBL:	EQU	MP+4
AHBL:	EQU	ALBL+2
ALBH:	EQU	AHBL+2
AHBH:	EQU	ALBH+2

START:
; 測試資料  1234H*5678H=? (6260060H) 

	MOV	MA,#12H
	MOV	MA+1,#34H
	MOV	MB,#56H
	MOV	MB+1,#78H

	SETB	F0
;   AL*BL
	MOV	A,MA+1
	MOV	B,MB+1
	MUL	AB
	MOV	ALBL,B
	MOV	ALBL+1,A
;   AH*BL
	MOV	A,MA
	MOV	B,MB+1
	MUL	AB
	MOV	AHBL,B
	MOV	AHBL+1,A
;   AL*BH
	MOV	A,MA+1
	MOV	B,MB
	MUL	AB
	MOV	ALBH,B
	MOV	ALBH+1,A
;   AH*BH
	MOV	A,MA
	MOV	B,MB
	MUL	AB
	MOV	AHBH,B
	MOV	AHBH+1,A
;
	MOV	MP+3,ALBL+1
;計算MP+2
	MOV	A,ALBL
	ADD	A,AHBL+1
	MOV	R6,A
	CLR	A
	ADDC	A,#0
	MOV	R7,A

	MOV	A,R6
	ADD	A,ALBH+1
	MOV	MP+2,A
	CLR	A
	ADDC	A,R7
;計算MP+1
	ADD	A,AHBL
	MOV	R6,A
	CLR	A
	ADDC	A,#0
	MOV	R7,A

	MOV	A,R6
	ADD	A,ALBH
	MOV	R6,A
	CLR	A
	ADDC	A,R7
	MOV	R7,A

	MOV	A,R6
	ADD	A,AHBH+1
	MOV	MP+1,A
	CLR	A
	ADDC	A,R7

;計算MP
	ADD	A,AHBH
	MOV	MP,A

	SETB	F0
	AJMP	$
	END
