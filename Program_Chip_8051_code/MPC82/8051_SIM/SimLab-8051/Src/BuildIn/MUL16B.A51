;
; 兩個16位元數的相乘(binary unsigned view) 
;
MA:	EQU	40H
MB:	EQU	MA+2
MP:	EQU	MB+2
MT:	EQU	MP+4

START:
; 測試資料  1234H*5678H=? (6260060H) 

	MOV	MA,#12H
	MOV	MA+1,#34H
	MOV	MB,#56H
	MOV	MB+1,#78H
	SETB	F0
;   t <- A
	MOV	MT,#0
	MOV	MT+1,#0
	MOV	MT+2,MA
	MOV	MT+3,MA+1
;   P <--B0*t
	MOV	A,MB+1
	JNB	ACC.0,NEXT1
;   P<----t
	MOV	MP,MT
	MOV	MP+1,MT+1
	MOV	MP+2,MT+2
	MOV	MP+3,MT+3
	AJMP	NEXT2
NEXT1:
;   P<----0
	MOV	MP,#0
	MOV	MP+1,#0
	MOV	MP+2,#0
	MOV	MP+3,#0
NEXT2:
	MOV	R7,#15     ; 重覆15次
LOOP1:
;   t <-t*2
	CLR	C
	MOV	A,MT+3
	RLC	A
	MOV	MT+3,A
	MOV	A,MT+2
	RLC	A
	MOV	MT+2,A
	MOV	A,MT+1
	RLC	A
	MOV	MT+1,A
	MOV	A,MT
	RLC	A
	MOV	MT,A
; B <-- shr B
	MOV	A,MB
	RRC	A
	MOV	MB,A
	MOV	A,MB+1
	RRC	A
	MOV	MB+1,A
;   check B0
	MOV	A,MB+1
	JNB	ACC.0,NEXT3
;   P<----P+t
	MOV	A,MT+3
	ADD	A,MP+3
	MOV	MP+3,A
	MOV	A,MT+2
	ADDC	A,MP+2
	MOV	MP+2,A
	MOV	A,MT+1
	ADDC	A,MP+1
	MOV	MP+1,A
	MOV	A,MT+0
	ADDC	A,MP+0
	MOV	MP+0,A
NEXT3:
	DJNZ	R7,LOOP1

	SETB	F0
	AJMP	$
	END
